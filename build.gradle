buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:0.6"
    classpath "org.gradle.api.plugins:gradle-nexus-plugin:0.7.1"
    classpath "com.github.ben-manes:gradle-versions-plugin:0.12.0"
  }
}
apply plugin: 'com.github.ben-manes.versions'
apply plugin: "com.jfrog.bintray"
apply plugin: "groovy"
apply plugin: "nexus"
apply from: "$rootDir/gradle/credentials.gradle"

group = "com.github.robfletcher"
version = "2.0.7"
archivesBaseName = "compass-gradle-plugin"
sourceCompatibility = 1.7

tasks.withType(JavaCompile) {
  sourceCompatibility = "1.7"
  targetCompatibility = "1.7"
}

tasks.withType(GroovyCompile) {
  sourceCompatibility = "1.7"
  targetCompatibility = "1.7"
}

repositories {
  jcenter()
  maven { url "https://plugins.gradle.org/m2/" }
}

dependencies {
  compile gradleApi()
  compile localGroovy()
  compile "com.github.jruby-gradle:jruby-gradle-plugin:2.0.0-alpha.7"
  compile "gradle.plugin.com.github.jengelman.gradle.plugins:gradle-processes:0.5.0"

  testCompile("com.google.guava:guava:21.0")
  testCompile("org.spockframework:spock-core:1.0-groovy-2.4") {
    exclude group: "org.codehaus.groovy"
  }
  testCompile("com.netflix.nebula:nebula-test:7.6.0") {
    exclude group: "org.codehaus.groovy"
  }
  testCompile "net.sourceforge.cssparser:cssparser:0.9.18"
}

test {
  systemProperties."compass.gem.dir" = file("${buildDir}/tmp/gems").canonicalPath
}

task sourceJar(type: Jar) {
  from sourceSets.main.allSource
}

bintray {
  user = project.bintrayUsername
  key = project.bintrayApiKey
  publish = true
  dryRun = false
  configurations = ["archives"]

  pkg {
    userOrg = "robfletcher"
    repo = "gradle-plugins"
    name = "compass-gradle-plugin"
    licenses = ["Apache-2.0"]
    labels = ["compass"]
    desc = "Gradle plugin for Sass & Compass compilation"

    version {
      name = project.version
      vcsTag = "v${project.version}"
      attributes = ["gradle-plugin": "com.github.robfletcher.compass:com.github.robfletcher:compass-gradle-plugin"]
    }
  }
}
bintrayUpload.dependsOn assemble

defaultTasks "build"
