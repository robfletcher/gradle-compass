buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:0.6"
    classpath "org.gradle.api.plugins:gradle-nexus-plugin:0.7.1"
  }
}
apply plugin: "com.jfrog.bintray"
apply plugin: "groovy"
apply plugin: "nexus"
apply from: "$rootDir/gradle/credentials.gradle"

group = "com.github.robfletcher"
version = "2.0.5"
archivesBaseName = "compass-gradle-plugin"

tasks.withType(JavaCompile) {
  sourceCompatibility = "1.7"
  targetCompatibility = "1.7"
}

tasks.withType(GroovyCompile) {
  sourceCompatibility = "1.7"
  targetCompatibility = "1.7"
}

repositories {
  jcenter()
}

dependencies {
  compile gradleApi()
  compile localGroovy()
  compile "com.github.jruby-gradle:jruby-gradle-plugin:0.1.9"
  compile "com.github.jengelman.gradle.plugins:gradle-processes:0.3.0"

  testCompile("org.spockframework:spock-core:0.7-groovy-2.0") {
    exclude group: "org.codehaus.groovy"
  }
  testCompile("com.netflix.nebula:nebula-test:2.0.4") {
    exclude group: "org.codehaus.groovy"
  }
  testCompile "net.sourceforge.cssparser:cssparser:0.9.14"
}

test {
  systemProperties."compass.gem.dir" = file("${buildDir}/tmp/gems").canonicalPath
}

task sourceJar(type: Jar) {
  from sourceSets.main.allSource
}

bintray {
  user = project.bintrayUsername
  key = project.bintrayApiKey
  publish = true
  dryRun = false
  configurations = ["archives"]

  pkg {
    userOrg = "robfletcher"
    repo = "gradle-plugins"
    name = "compass-gradle-plugin"
    licenses = ["Apache-2.0"]
    labels = ["compass"]
    desc = "Gradle plugin for Sass & Compass compilation"

    version {
      name = project.version
      vcsTag = "v${project.version}"
      attributes = ["gradle-plugin": "com.github.robfletcher.compass:com.github.robfletcher:compass-gradle-plugin"]
    }
  }
}
bintrayUpload.dependsOn assemble

defaultTasks "build"
