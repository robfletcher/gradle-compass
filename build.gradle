buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.6"
    classpath "org.gradle.api.plugins:gradle-nexus-plugin:0.7.1"
    classpath "com.github.ben-manes:gradle-versions-plugin:0.12.0"
  }
}
apply plugin: 'com.github.ben-manes.versions'
apply plugin: "com.jfrog.bintray"
apply plugin: "groovy"
apply plugin: "nexus"
apply plugin: "maven-publish"
apply from: "$rootDir/gradle/credentials.gradle"

group = "com.github.robfletcher"
version = "2.0.7"
archivesBaseName = "compass-gradle-plugin"
sourceCompatibility = 1.7

tasks.withType(JavaCompile) {
  sourceCompatibility = "1.7"
  targetCompatibility = "1.7"
}

tasks.withType(GroovyCompile) {
  sourceCompatibility = "1.7"
  targetCompatibility = "1.7"
}

repositories {
  jcenter()
}

dependencies {
  compile gradleApi()
  compile localGroovy()
  compile "com.github.jruby-gradle:jruby-gradle-plugin:1.2.0"
  compile "com.github.jengelman.gradle.plugins:gradle-processes:0.3.0"

  testCompile("org.spockframework:spock-core:1.0-groovy-2.4") {
    exclude group: "org.codehaus.groovy"
  }
  testCompile("com.netflix.nebula:nebula-test:4.0.0") {
    exclude group: "org.codehaus.groovy"
  }
  testCompile "net.sourceforge.cssparser:cssparser:0.9.18"
}

test {
  systemProperties."compass.gem.dir" = file("${buildDir}/tmp/gems").canonicalPath
}

task sourceJar(type: Jar) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

publishing {
  publications {
    mavenGroovy(MavenPublication) {
      from components.java
      
      artifact sourceJar

      pom.withXml {
        Node license = new Node(null, 'license')
        license.appendNode('name', 'Apache License, Version 2.0')
        license.appendNode('url', 'https://www.apache.org/licenses/LICENSE-2.0.txt')
        license.appendNode('distribution', 'repo')
        
        asNode().appendNode('licenses').append(license)
      }
    }
  }
}

bintray {
  user = project.bintrayUsername
  key = project.bintrayApiKey
  publish = true
  dryRun = false
  publications = ['mavenGroovy']

  pkg {
    userOrg = "robfletcher"
    repo = "gradle-plugins"
    name = "compass-gradle-plugin"
    licenses = ["Apache-2.0"]
    labels = ["compass"]
    desc = "Gradle plugin for Sass & Compass compilation"
	
    websiteUrl = 'https://github.com/robfletcher/gradle-compass'
    issueTrackerUrl = 'https://github.com/robfletcher/gradle-compass/issues'
    vcsUrl = 'https://github.com/robfletcher/gradle-compass.git'

    githubRepo = 'robfletcher/gradle-compass'
    githubReleaseNotesFile = 'README.adoc'

    version {
      name = project.version
      vcsTag = "v${project.version}"
      attributes = ["gradle-plugin": "com.github.robfletcher.compass:com.github.robfletcher:compass-gradle-plugin"]
    }
  }
}
bintrayUpload.dependsOn assemble

task wrapper(type: Wrapper) { gradleVersion = '2.13' }

defaultTasks "build"
